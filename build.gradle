apply plugin: 'cpp'

wrapper {
	gradleVersion = '4.3'
}

model {
	platforms {
		x86_64 {
			architecture "x86_64"
		}

		binaryen {
			architecture "binaryen"
		}

		asmjs {
			architecture "asmjs"
		}
	}

	toolChains {
		gcc (Gcc) {
			target("x86_64") {
				cppCompiler.withArguments { args -> 
					args << "-std=c++14"
					args << "-O0"
					args << "-g"
				}

				linker.withArguments { args -> 
					args << "-O0"
					args << "-lGLESv2"
					args << "-lglfw"
					args << "-lopenal"					
				}
			}
		}

		Emscripten(Clang) {
			eachPlatform {
				cppCompiler.executable = 'em++'
				linker.executable = 'em++'

				cppCompiler.withArguments { args -> 
					args << "-std=c++14"
					args << "-O2"
					args << "-s"
					args << "USE_GLFW=3"
					args << "-s"
					args << "USE_WEBGL2=1"
				}

				linker.withArguments { args -> 
					args[1] += ".html"
					args.remove "-m64"
					args << "-O2"
					args << "-s"
					args << "USE_GLFW=3"
					args << "-s"
					args << "USE_WEBGL2=1"
					args << "--preload-file"
					args << "data"
				}
			}

			target ("asmjs")

			target("binaryen") {				
				cppCompiler.withArguments { args ->					
					args << "-s"
					args << "BINARYEN=1"
				}

				linker.withArguments { args ->					
					args << "-s"
					args << "BINARYEN=1"
				}
			}
		}
	}	

	components {
		ws(NativeExecutableSpec) {
			targetPlatform "binaryen"
			targetPlatform "asmjs"
			targetPlatform "x86_64"

			sources {
				cpp {
					source {
						srcDir 'src/main/cpp'
						include '**/*.cpp'
					}

					exportedHeaders {
						srcDir 'src/main/include'
						include '**/*.hpp'
					}
				}
			}
		}
	}
}

task emscriptenUpdate (type: Exec) {
	executable "emsdk-portable/emsdk"
	args "update"
}

task emscriptenInstall (type: Exec) {
	dependsOn emscriptenUpdate
	executable "emsdk-portable/emsdk"
	args "install", "latest"
}

task emscriptenActivate (type: Exec) {
	dependsOn emscriptenInstall
	executable "emsdk-portable/emsdk"
	args "activate", "latest"
}

task emscripten {
	dependsOn emscriptenActivate
}

task runAsmjs {
	dependsOn build		

	doLast {		
		java.awt.Desktop.getDesktop().browse(new java.io.File("build/exe/ws/asmjs/ws.html").toURI())
	}
}

task runBinaryen {
	dependsOn build

	doLast {
		java.awt.Desktop.getDesktop().browse(new java.io.File("build/exe/ws/binaryen/ws.html").toURI())
	}
}

task runNative {
	dependsOn build

	doLast {
		"build/exe/ws/x86_64/ws".execute()
	}
}

task valgrind {
	dependsOn build

	doLast {
		"valgrind build/exe/ws/x86_64/ws".execute()
	}
}