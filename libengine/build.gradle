apply from: rootProject.file('share/angle.gradle')
apply from: rootProject.file('share/glfw3.gradle')
apply from: rootProject.file('share/openal_soft.gradle')

model {
    flavors {
        GLES20
        GLES30
        GL
    }
    
    components {
        engine(NativeLibrarySpec) {
            targetPlatform 'asmjs'
            targetPlatform 'binaryen'
            targetPlatform 'linux32'
            targetPlatform 'linux64'
            targetPlatform 'windows32'
            targetPlatform 'windows64'

            sources {
                cpp {
                    lib library: 'glfw3'
                    lib library: 'openal_soft'
                    lib library: 'angle'
                    lib project: ':libgraphics', library: 'graphics'
                    lib project: ':libaudio', library: 'audio'                  
                    
                    source {
                        srcDir 'src/main/cpp'
                        include '**/*.cpp'
                    }

                    exportedHeaders {
                        srcDirs '../share/src/main/include', 'src/main/include'
                        include '**/*.hpp'
                    }
                }
            }

            binaries.all {
                if (flavor == flavors.GLES20) {
                    cppCompiler.define 'GLES20'
                } else if (flavor == flavors.GLES30) {
                    cppCompiler.define 'GLES30'
                } else if (flavor == flavors.GL) {
                    cppCompiler.define 'GL'

                    if (targetPlatform.operatingSystem.windows) {
                        linker.args '-lopengl32'
                    }                    
                }
            }
        }
    }
}
