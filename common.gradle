apply plugin: 'cpp'

task wrapper(type: Wrapper) {
    gradleVersion = '4.2'
}

model {
	platforms {
		linux32 {
			operatingSystem "linux"
			architecture "x86"
		}

		linux64 {
			operatingSystem "linux"
			architecture "x86_64"
		}

		binaryen {
			operatingSystem "webassembly"
			architecture "binaryen"
		}

		asmjs {
			operatingSystem "webassembly"
			architecture "asmjs"
		}

		windows32 {
			operatingSystem "windows"
			architecture "x86"
		}

		windows64 {
			operatingSystem "windows"
			architecture "x86_64"
		}
	}

	toolChains {
		gcc (Gcc) {
			eachPlatform {
				cppCompiler.withArguments { args -> 
					args.add(0, "$CXX_STD")
					args.add(0, "$CXX_OPT")
					args.add(0, "-g")
				}

				linker.withArguments { args -> 
					args.add(0, "$CXX_OPT")

					int dummyIdx = -1;
					for (int i = 0; i < args.size(); i++) {
						if (args[i].endsWith('dummy.so') || args[i].endsWith('dummy.dll')) {
							dummyIdx = i;
						}
					}
					
					if (dummyIdx != -1) {
						args.remove(dummyIdx);
					}
				}
			}

			target("linux64") {
				cCompiler.executable = 'x86_64-linux-gnu-gcc'
				cppCompiler.executable = 'x86_64-linux-gnu-g++'
				linker.executable = 'x86_64-linux-gnu-g++'
				assembler.executable = 'x86_64-linux-gnu-as'
				staticLibArchiver.executable = 'x86_64-linux-gnu-ar'				
			}

			target("linux32") {
				cCompiler.executable = 'i686-linux-gnu-gcc'
				cppCompiler.executable = 'i686-linux-gnu-g++'
				linker.executable = 'i686-linux-gnu-g++'
				assembler.executable = 'i686-linux-gnu-as'
				staticLibArchiver.executable = 'i686-linux-gnu-ar'
			}

			target("windows32") {
				cCompiler.executable = 'i686-w64-mingw32-gcc'
				cppCompiler.executable = 'i686-w64-mingw32-g++'
				linker.executable = 'i686-w64-mingw32-g++'
                assembler.executable = 'i686-w64-mingw32-as'
                staticLibArchiver.executable = 'i686-w64-mingw32-ar'

				cppCompiler.withArguments { args ->
					args << "-DGLFW_DLL"
					args << "-DUSE_EGL"
				}

				linker.withArguments { args -> 
					args << '-static'
				}
			}

			target("windows64") {
				cCompiler.executable = 'x86_64-w64-mingw32-gcc'
				cppCompiler.executable = 'x86_64-w64-mingw32-g++'
                linker.executable = 'x86_64-w64-mingw32-g++'
                assembler.executable = 'x86_64-w64-mingw32-as'
                staticLibArchiver.executable = 'x86_64-w64-mingw32-ar'

				cppCompiler.withArguments { args ->
					args << "-DGLFW_DLL"
					args << "-DUSE_EGL"
				}

				linker.withArguments { args -> 
					args << '-static'
				}
			}
		}

		emscripten (Clang) {
			eachPlatform {				
				staticLibArchiver.withArguments { args -> 
                    args.remove '-rcs'
                    args.add(0, "$CXX_OPT")
                    args.add(0, '-emit-llvm')
					

                    int outputIdx;                    
                    for (outputIdx = 0; outputIdx < args.size(); outputIdx++) {
                        if (args[outputIdx].endsWith('.a')) {                            
                            args[outputIdx] = args[outputIdx].replace('.a', '.bc')
                            args.add(outputIdx, '-o')
                            break;
                        }
                    }                    
				}

				cppCompiler.withArguments { args -> 
					args.add(0, "$CXX_STD")
					args.add(0, "$CXX_OPT")
					args.add(0, '-emit-llvm')
				}

				linker.withArguments { args -> 
					args.remove '-m64'
					args.add(0, '-O2')

					int dummyIdx = -1;
					for (int i = 0; i < args.size(); i++) {
						args[i] = args[i].replace('.a', '.bc')
                        
                        if (args[i] == '-o') {
							if (!(args[i+1].endsWith('.so'))) {
								args[i+1] += '.html'
							}                            
                        }

						if (args[i].endsWith('dummy.so')) {
							dummyIdx = i;
						}
					}
					
					if (dummyIdx != -1) {
						args.remove(dummyIdx);
					}
				}
			}

			target ("asmjs") {
				cppCompiler.executable = 'em++'
				linker.executable = 'em++'
				staticLibArchiver.executable = 'em++'
			}

			target("binaryen") {
				cppCompiler.executable = 'em++'
				linker.executable = 'em++'
				staticLibArchiver.executable = 'em++'

				staticLibArchiver.withArguments { args ->
					args << '-s'
					args << 'BINARYEN=1'
				}

				cppCompiler.withArguments { args ->					
					args << "-s"
					args << "BINARYEN=1"
				}

				linker.withArguments { args ->					
					args << "-s"
					args << "BINARYEN=1"
				}
			}
		}
	}
}
	
