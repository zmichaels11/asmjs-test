apply plugin: 'cpp'

task wrapper(type: Wrapper) {
    gradleVersion = '4.2'
}

model {
	platforms {
		x86_64 {
			architecture "x86_64"
		}

		binaryen {
			architecture "binaryen"
		}

		asmjs {
			architecture "asmjs"
		}

		win64 {
			operatingSystem "windows"
			architecture "x86_64"
		}
	}

	toolChains {
		mingw (Gcc) {
			target("win64") {
				cppCompiler.executable = 'x86_64-w64-mingw32-g++'
                linker.executable = 'x86_64-w64-mingw32-g++'
                assembler.executable = 'x86_64-w64-mingw32-as'
                staticLibArchiver.executable = 'x86_64-w64-mingw32-ar'
			}
		}

		gcc (Gcc) {
			target("x86_64") {				
				cppCompiler.withArguments { args -> 
					args.add(0, "$CXX_STD")
					args.add(0, "$CXX_OPT")
					args.add(0, "-g")
				}

				linker.withArguments { args -> 
					args.add(0, "$CXX_OPT")
				}
			}
		}

		emscripten (Clang) {
			eachPlatform {
				cppCompiler.executable = 'em++'
				linker.executable = 'em++'
				staticLibArchiver.executable = 'em++'

				staticLibArchiver.withArguments { args -> 
                    args.remove '-rcs'
                    args.add(0, "$CXX_OPT")
                    args.add(0, '-emit-llvm')

                    int outputIdx;                    
                    for (outputIdx = 0; outputIdx < args.size(); outputIdx++) {
                        if (args[outputIdx].endsWith('.a')) {                            
                            args[outputIdx] = args[outputIdx].replace('.a', '.bc')
                            args.add(outputIdx, '-o')
                            break;
                        }
                    }                    
				}

				cppCompiler.withArguments { args -> 
					args.add(0, "$CXX_STD")
					args.add(0, "$CXX_OPT")
					args.add(0, '-emit-llvm')
				}

				linker.withArguments { args -> 
					args.remove '-m64'
					args.add(0, '-O2')

					for (int i = 0; i < args.size(); i++) {
						args[i] = args[i].replace('.a', '.bc')
                        
                        if (args[i] == '-o') {
                            args[i+1] += '.html'
                        }
					}
				}
			}

			target ("asmjs") {}

			target("binaryen") {
				staticLibArchiver.withArguments { args ->
					args << '-s'
					args << 'BINARYEN=1'
				}

				cppCompiler.withArguments { args ->					
					args << "-s"
					args << "BINARYEN=1"
				}

				linker.withArguments { args ->					
					args << "-s"
					args << "BINARYEN=1"
				}
			}
		}
	}	

	repositories {
		libs (PrebuiltLibraries) {
			glfw3 {
				headers.srcDir file(projectDir.path + '/lib/glfw3/win64/include')
				binaries.withType(SharedLibraryBinary) {
					sharedLibraryFile = file(projectDir.path + '/lib/glfw3/win64/glfw3.dll')
				}
			}
		}
	}
}
	
